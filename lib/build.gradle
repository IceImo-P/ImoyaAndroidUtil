plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

def libVersionName = "1.6.2"

android {
    namespace = 'net.imoya.android.util'

    compileSdk 29

    defaultConfig {
        minSdk 19
        targetSdk 29
        versionCode 3
        versionName "$libVersionName"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        aarMetadata {
            minCompileSdk = 29
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId "net.imoya.android.util"
                artifactId "imoya-android-util"
                version "$libVersionName"

                pom {
                    name = "ImoyaAndroidUtil"
                    url = "https://github.com/IceImo-P/ImoyaAndroidUtil"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/IceImo-P/ImoyaAndroidUtil")
                credentials {
                    /** Create github.properties in root project folder file with
                     ** gpr.user=GITHUB_USER_ID & gpr.token=PERSONAL_ACCESS_TOKEN
                     ** Set env variable GPR_USER & GPR_TOKEN if not adding a properties file**/
                    username = githubProperties['gpr.user'] ?: System.getenv("GPR_USER")
                    password = githubProperties['gpr.token'] ?: System.getenv("GPR_TOKEN")
                }
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'net.imoya.android.log:imoya-android-log:1.1.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.11.2'
    testImplementation 'org.mockito:mockito-inline:3.11.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}